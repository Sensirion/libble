apply plugin: 'com.android.library'
apply plugin: 'maven'

version = "1.0.1"
def code = 23

group = 'com.sensirion'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    defaultConfig {
        archivesBaseName = 'libble'
        minSdkVersion 18
        targetSdkVersion 23
        versionName version
        versionCode code
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFile '../proguard.cfg'
        }
    }
    libraryVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.aar')) {
                    def fileName = "${archivesBaseName}-${version}.aar"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.1.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task writePom << {
    pom {
        artifactId = 'libble'
        project {
            inceptionYear '2014'
            packaging 'aar'
            licenses {
                license {
                    name 'BSD 3-Clause'
                    url 'http://opensource.org/licenses/BSD-3-Clause'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("${buildDir}/libs/${project.archivesBaseName}-${project.version}.pom")
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
